apply from: rootProject.file('gradle/dependencies.gradle')
apply from: rootProject.file('gradle/repositories.gradle')
apply from: rootProject.file('gradle/profile.gradle')
apply from: rootProject.file('gradle/plugins.gradle')
apply from: rootProject.file('gradle/publish.gradle')

buildscript { scriptHandler ->
	apply from: rootProject.file('gradle/dependencies.gradle')
	apply from: rootProject.file('gradle/repositories.gradle'), to: scriptHandler
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "gradle.plugin.org.detoeuf:swagger-codegen-plugin:${swaggerCodegenPluginVersion}"
	}
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
	main {
		java {
			srcDir file("${projectDir}/src/main/java")
		}
	}
	generated {
		java {
			srcDir file("${projectDir}/src/main/java/generated")
		}
	}
}

idea {
	module {
		sourceDirs += file("${projectDir}/src/main/java/generated")
		testSourceDirs += file("${projectDir}/src/test/generated")
	}
}

jar {
	baseName = "${baseArtifact}"
	version = "${baseVersion}"
}

bootRun {
	if (profile != null) {
		args = ["--spring.profiles.active=" + profile]
	}

	addResources = true
}

swagger {
	inputSpec = 'https://demo.api-v2.vroong.com/v2/api-docs'

	output = "${projectDir}/vroong-api-client"
	language = 'java'

	additionalProperties = [
			'groupId'           : 'com.vroong',
			'artifactId'        : 'api-client',
			'artifactVersion'   : '1.0.0',
			'modelPackage'      : 'com.vroong.api.client.model',
			'apiPackage'        : 'com.vroong.api.client.api',
			'dateLibrary'       : 'java8'
	]
}

dependencies {
	compile "net.meshkorea:platform-core-web:${coreWebVersion}"

	// Swagger
	compile "io.springfox:springfox-swagger2:${springfoxVersion}"
	compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"

	compile "com.mysema.querydsl:querydsl-jpa:${mysemaQuerydslVersion}"
	compile "com.mysema.querydsl:querydsl-apt:${mysemaQuerydslVersion}:jpa"

	compileOnly "org.springframework.boot:spring-boot-configuration-processor"

	testCompile "org.springframework.boot:spring-boot-starter-test"
	testCompile "org.springframework.security:spring-security-test"
}

compileJava {
	options.compilerArgs << "-s"
	options.compilerArgs << "${projectDir}/src/main/java/generated"

	doFirst {
		file("${projectDir}/src/main/java/generated").mkdirs()
	}
}

clean.doLast {
	file("${projectDir}/src/main/java/generated").deleteDir()
	file("${projectDir}/vroong-api-client").deleteDir()
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.4'
}
