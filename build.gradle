apply from: rootProject.file('gradle/dependencies.gradle')
apply from: rootProject.file('gradle/repositories.gradle')
apply from: rootProject.file('gradle/profile.gradle')
apply from: rootProject.file('gradle/plugins.gradle')
apply from: rootProject.file('gradle/publish.gradle')

buildscript { scriptHandler ->
    apply from: rootProject.file('gradle/dependencies.gradle')
    apply from: rootProject.file('gradle/repositories.gradle'), to: scriptHandler
    dependencies {
        classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslGradlePluginVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "net.ltgt.gradle:gradle-apt-plugin:${aptPluginVersion}"
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def queryDslOutput = file("${projectDir}/src/main/generated")

sourceSets {
    generated
    main {
        java {
            srcDirs += queryDslOutput
        }
    }
    generated {
        java {
            srcDirs += queryDslOutput
        }
    }
    test {
        java {
            srcDirs += queryDslOutput
        }
    }
}

querydsl {
    querydslSourcesDir = queryDslOutput
    jpa = true
}

jar {
    baseName = "${baseArtifact}"
    version = "${baseVersion}"
}

bootRun {
    if (profile != null) {
        args = ["--spring.profiles.active=" + profile]
    }

    addResources = true
}

dependencies {
    compile "net.meshkorea:platform-core-web:${coreWebVersion}"

    compile "com.vroong:vroong-api-client:${vroongApiClientVersion}"
    compile "com.vroong:vroong-admin-api-client:${vroongAdminApiClientVersion}"
    compile "com.meshprime:prime-api-client:${primeApiClientVersion}"

    compile "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}"

    compile "org.apache.httpcomponents:fluent-hc:${apacheHttpcomponentsVersion}"

    // Swagger
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"

    // querydsl
    compile "com.querydsl:querydsl-core:${querydslVersion}"
    compile "com.querydsl:querydsl-apt:${querydslVersion}"
    compile "com.querydsl:querydsl-jpa:${querydslVersion}"

    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile "org.springframework.security.oauth:spring-security-oauth2:${springSecurityOauth2Version}"
    compile "org.springframework.security:spring-security-jwt:${springSecurityJwtVersion}"

    compile "com.auth0:java-jwt:${javaJwtVersion}"

    compile files("${projectDir}/libs/ICERTSecu_JDK18.jar")

    compile "org.apache.poi:poi:${poiVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"

    compile "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"

    apt "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    apt "org.projectlombok:lombok:${lombokVersion}"

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.security:spring-security-test"
}

compileJava {
    options.compilerArgs = [
        '-Amapstruct.suppressGeneratorTimestamp=true',
        '-Amapstruct.suppressGeneratorVersionInfoComment=true'
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = "${gradleWrapperVersion}"
}
